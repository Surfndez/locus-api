// apply plugins
apply plugin: 'java'
apply plugin: "kotlin"

// set Java 8 compatibility
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + KOTLIN_VERSION
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task createSourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task createJavadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// generate publication in Maven format
// https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
publishing {
    publications {
        Release(MavenPublication) {
            groupId 'com.asamm'
            artifactId 'locus-api-core'
            //noinspection GroovyAssignabilityCheck
            version API_VERSION

            // define source
            from components.java
            artifact createSourceJar
            artifact createJavadocJar

            // improve generated POM file
            pom.withXml {
                asNode().appendNode(
                        'name', 'Locus API')
                asNode().appendNode(
                        'description', 'Core library for Android "Locus Map" application')
                asNode().appendNode(
                        'url', 'https://github.com/asamm/locus-api')
                asNode().appendNode(
                        'inceptionYear', '2019')

                // add license
                def nodeLicenses = asNode().appendNode('licenses')
                def nodeLicense = nodeLicenses.appendNode('license')
                nodeLicense.appendNode('name', 'GNU Lesser General Public License v3.0')
                nodeLicense.appendNode('url', 'https://api.github.com/licenses/lgpl-3.0')
                nodeLicense.appendNode('distribution', 'repo')

                // add "scm"
                def nodeScm = asNode().appendNode('scm')
                nodeScm.appendNode('connection', 'git://github.com/asamm/locus-api.git')
                nodeScm.appendNode('url', 'git://github.com/asamm/locus-api.git')

                // add "developer info"
                def nodeDevelopers = asNode().appendNode('developers')
                def nodeDeveloper = nodeDevelopers.appendNode('developer')
                nodeDeveloper.appendNode('name', 'Asamm software, s. r. o.')
            }
        }
    }
}